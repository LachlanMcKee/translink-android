apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'

def addStringProperty(config, String javaName, String resName, String value) {
    config.buildConfigField "String", javaName, "\"$value\""
    config.resValue "string", resName, value
}

repositories {
    maven {
        url "https://mint.splunk.com/gradle/"
    }
}

dependencies {
    //noinspection GradleDependency
    compile 'com.google.android.gms:play-services-maps:6.5.87'

    compile 'com.android.support:appcompat-v7:23.1.1' // TODO: Once they fix the dialog accent colour bug.
    compile 'com.android.support:cardview-v7:23.1.1'
    compile('com.android.support:design:23.1.1')
    compile 'com.android.support:recyclerview-v7:23.1.1'

    compile 'com.squareup.okhttp:okhttp:2.5.0'

    compile 'de.greenrobot:eventbus:2.4.1'

    compile "com.splunk.mint:mint:4.2.1"

    // DI
    compile 'com.jakewharton:butterknife:6.1.0'

    compile 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion as Integer
    buildToolsVersion rootProject.ext.buildToolsVersion as String

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion as Integer
        targetSdkVersion rootProject.ext.targetSdkVersion as Integer

        addStringProperty(delegate, "PREF_THEME_KEY", "theme_pref_key", "themeKey")
        addStringProperty(delegate, "THEME_VALUE_LIGHT", "theme_light", "light")
        addStringProperty(delegate, "THEME_VALUE_DARK", "theme_dark", "dark")
    }

    dataBinding {
        enabled = true
    }
}
